import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
    id 'com.google.protobuf'
}


android {
    namespace 'de.stustapay.stustapay'
    compileSdk 35

    defaultConfig {
        applicationId "de.stustapay.stustapay"
        minSdk 28
        targetSdk 33
        versionCode getSSPVersionCode()
        versionName getSSPVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            if (project.hasProperty('ANDROID_KEY_STORE_FILE')) {
                storeFile file(ANDROID_KEY_STORE_FILE)
                storePassword ANDROID_KEY_STORE_PASSWORD
                keyAlias ANDROID_KEY_ALIAS
                keyPassword ANDROID_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty('ANDROID_KEY_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility = targetJavaVersion
        targetCompatibility = targetJavaVersion
    }
    kotlinOptions {
        jvmTarget = targetJavaVersion.toString()
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}


dependencies {
    implementation project(":api")
    implementation project(":libssp")
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.activity:activity-compose:$activity_compose_version"
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_ui_version"
    implementation "androidx.compose.material:material-icons-core:$compose_ui_version"
    implementation "androidx.navigation:navigation-compose:$navigation_compose_version"
    implementation "androidx.preference:preference-ktx:$preference_ktx_version"
    implementation "androidx.camera:camera-core:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"
    implementation 'com.google.zxing:core:3.5.1'
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation 'androidx.databinding:databinding-runtime:8.8.2'
    implementation "androidx.datastore:datastore:1.1.3"
    implementation 'com.sumup:merchant-api:1.4.0'
    implementation 'com.sumup:merchant-sdk:5.0.3'
    implementation 'net.openid:appauth:0.11.1'
    implementation "com.google.protobuf:protobuf-javalite:$protobuf_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"
    annotationProcessor "androidx.hilt:hilt-compiler:$hilt_android_version"
    ksp "androidx.hilt:hilt-compiler:$hilt_android_version"
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_android_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    implementation "com.ionspin.kotlin:bignum:$bignum_version"
    implementation "com.ionspin.kotlin:bignum-serialization-kotlinx:$bignum_version"
}

composeCompiler {
    reportsDestination = layout.buildDirectory.dir("compose_compiler")
    stabilityConfigurationFile = rootProject.layout.projectDirectory.file("stability_config.conf")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

android.defaultConfig.manifestPlaceholders = [
        'appAuthRedirectScheme': 'de.stustapay.stustapay'
]
